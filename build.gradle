buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.org.flywaydb:gradle-plugin-publishing:6.2.0"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.flywaydb.flyway'

group 'com.todo.exmaple'
version '0.0.1'
mainClassName = "io.ktor.server.tomcat.EngineMain"

task fatJar(type: Jar) {
    manifest {
        attributes "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": mainClassName
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    with jar
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-tomcat:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"

    implementation "org.jetbrains.exposed:exposed:0.17.7"
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation "org.postgresql:postgresql:42.2.14"

    implementation 'org.flywaydb:flyway-core:6.5.3'

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
}

def DATABASE_URL = System.getenv()['DATABASE_URL'] == null ? "jdbc:postgresql://localhost:5432/todo" : System.getenv()['DATABASE_URL']
def DATABASE_USER = System.getenv()['DATABASE_USER'] == null ? "ktoruser" : System.getenv()['DATABASE_USER']
def DATABASE_PASSWORD = System.getenv()['DATABASE_PASSWORD'] == null ? "ktorpass" : System.getenv()['DATABASE_PASSWORD']

flyway {
    url = DATABASE_URL
    user = DATABASE_USER
    password = DATABASE_PASSWORD
    baselineOnMigrate=true
    locations = ["filesystem:resources/db/migration"]
}